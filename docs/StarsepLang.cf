-- Programs ------------------------------------------------

entrypoints   Program ;
Program.      Program ::= [FnDef] ;

FnDef.	      FnDef ::= Type Ident "(" [Arg] ")" Block ;
separator nonempty FnDef "" ;

Arg.          Arg ::= Type Ident ;
separator Arg "," ;

-- Statements ----------------------------------------------

Block.        Block ::= "{" [Stmt] "}" ;
BStmt.        Stmt ::= Block ;
separator     Stmt "" ;

Decl.         Oper ::= Type [Item] ;
NoInit.       Item ::= Ident ;
Init.         Item ::= Ident "=" Expr ;
separator nonempty Item "," ;

Let.          Oper ::= "let" [Item] ;
Ass.          Oper ::= Ident AssOp Expr ;
Incr.         Oper ::= Ident "++" ;
Decr.         Oper ::= Ident "--" ;
Ret.          Oper ::= "return" Expr ;
VRet.         Oper ::= "return" ;
FnExec.       Oper ::= Ident "(" [Expr] ")" ;
Print.        Oper ::= "print" "(" Expr ")" ;
Assert.       Oper ::= "assert" "(" Expr ")" ;

OperStmt.     Stmt ::= Oper ";" ;
While.        Stmt ::= "while" Expr Block ;
For.          Stmt ::= "for" Oper ";" Expr ";" Oper Block ;
Foreach.      Stmt ::= "for" Ident "in" Expr Block ;
Loop.         Stmt ::= "loop" Block ;

IfElifStmt.    IfStmt ::= IfStmt "elif" Expr Block;
IfElseStmt.    IfElseStmt ::= IfStmt "else" Block ;
IfStmt.        IfStmt ::= "if" Expr Block ;

CondIf.       Stmt   ::= IfStmt ;
ElseStmt.     Stmt   ::= IfElseStmt ;

-- Types ---------------------------------------------------

Int.          Type ::= "int" ;
Char.         Type ::= "char" ;
Str.          Type ::= "string" ;
Bool.         Type ::= "bool" ;
Float.        Type ::= "float" ;
Void.         Type ::= "void" ;
TypeOf.       Type ::= "typeof" "(" Expr ")" ;
FnType.       Type ::= "Fn" "<" [Type] ">" ;
separator nonempty Type "->" ;

-- Expressions ---------------------------------------------

EFun.         Expr8 ::= Ident "(" [Expr] ")" ;
EVar.         Expr7 ::= Ident ;
EInt.         Expr7 ::= Integer ;
EChar.        Expr7 ::= Char ;
EFloat.       Expr7 ::= Double ;
EString.      Expr7 ::= String ;
EFalse.       Expr7 ::= "false" ;
ETrue.        Expr7 ::= "true" ;
ENeg.         Expr6 ::= "-" Expr7 ;
ENot.         Expr6 ::= "!" Expr7 ;
EMul.         Expr5 ::= Expr5 MulOp Expr6 ;
EAdd.         Expr4 ::= Expr4 AddOp Expr5 ;
ERel.         Expr3 ::= Expr3 RelOp Expr4 ;
EAnd.         Expr2 ::= Expr3 "&&" Expr2 ;
EOr.          Expr1 ::= Expr2 "||" Expr1 ;
ETernary.     Expr  ::= Expr1 "?" Expr1 ":" Expr1 ;

coercions     Expr 8 ;
separator     Expr "," ;

-- Lambdas -------------------------------------------------

Lambda.       Expr ::= "@" "(" [Arg] ")" "->" Expr ;

-- Operators -----------------------------------------------

Plus.         AddOp ::= "+" ;
Minus.        AddOp ::= "-" ;
Times.        MulOp ::= "*" ;
Div.          MulOp ::= "/" ;
Mod.          MulOp ::= "%" ;
LTH.          RelOp ::= "<" ;
LE.           RelOp ::= "<=" ;
GTH.          RelOp ::= ">" ;
GE.           RelOp ::= ">=" ;
EQU.          RelOp ::= "==" ;
NE.           RelOp ::= "!=" ;
Assign.       AssOp ::= "=" ;
PlusAss.      AssOp ::= "+=" ;
MinusAss.     AssOp ::= "-=" ;
MulAss.       AssOp ::= "*=" ;
DivAss.       AssOp ::= "/=" ;
ModAss.       AssOp ::= "%=" ;

-- Comments ------------------------------------------------

comment       "#" ;
comment       "//" ;
comment       "/*" "*/" ;
